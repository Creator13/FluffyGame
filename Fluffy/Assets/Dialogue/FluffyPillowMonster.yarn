title: MayIEnter
tags:
colorID: 0
position: 0,0
---
Robin: Hello? Is anybody home?
PillowMonster: What do you need? 
PillowMonster: It's 3am. You woke me up. 
-> Can I come in?
    PillowMonster: It's already too crowded in here. What for? 
-> I need help!
    PillowMonster: Why do you think I can help you? 
Robin: I lost Fluffy. I only found his ear! // mild panic
PillowMonster: Sure, sure. Come in...
===
title: TheBargain
tags: 
colorID: 0
position: 50, 0
---
<<if visited("TheBargain") is true>>
    <<jump IHavePillows>>
<<endif>>
PillowMonster: So what's the matter?
Robin: Have you seen Fluffy? Or some part of him maybe?
PillowMonster: What does he look like?
-> He is green.
    <<SetFluffyProperty color green>>
-> He is pink.
    <<SetFluffyProperty color pink>>
-> He is orange.
    <<SetFluffyProperty color orange>>
Robin: Could you take a look at his ear maybe?
<<ShowItem fluffyEar 2>>
PillowMonster: What a beautiful earpiece you have there.
PillowMonster: Beautifully woven... and such vibrant colors! 
PillowMonster: It just so happens that I lost a piece of my fort recently.
-> Ask what happened
    PillowMonster: How would I know?? 
    PillowMonster: Someone just keeps stealing my pillows, the most beautiful ones even!
    -> Be kind
        Robin: I'm so sorry for you. 
        Robin: I know what it's like to lose something.
        Robin: I lost my best friend
    -> Be mean
        Robin: Get over yourself. 
        Robin: Pillows aren't even alive and I lost my BEST friend
-> Don't ask what happened
PillowMonster: Maybe I do have some information about Fluffy.
PillowMonster: I will give it to you if you give me that piece of wonderful ear.
Robin: I can't give you Fluffy's ear! 
Robin: How would he know when to come if he's called?!
PillowMonster: You can't call him now either, can you?
Robin: Well...
PillowMonster: Give it to me! 
-> Bring pillows.
    Robin: What if I found you some pillows for your fort?
    Robin: I'll look around the house for the brightest colors!
    PillowMonster: I suppose that will do.
    PillowMonster: I will need at least {$pillows_needed}!
-> Stay away!
    Robin: Stay away from Fluffy's ear! 
    Robin: I will take action if you come close!
    <<wait 1>>
    PillowMonster: Fine.
    PillowMonster: You'll need to bring me {$pillows_needed} pillows!
    PillowMonster: Then I'll talk.
<<ActivatePillowSearch>>
===
title: IHavePillows
tags:
colorID: 0
position: 100, 0
---
// misschien hier dat je als speler kan kiezen hoeveel kussens je hem geeft of welke? en gingen we ook nog doen dat je dus daadwerkelijk kan zoeken op bepaalde plekken?
<<if (pillow_count() < $pillows_needed)>>
    PillowMonster: I'll need more pillows than that! 
    <<stop>>
<<endif>>
Robin: I've found all these pillows!
Robin: Look how beautiful they are!
PillowMonster: Hm. They're fine.
PillowMonster: This one's shiny. I love it!! I mean, it's fine. They're fine.
PillowMonster: I will tell you what I know.
PillowMonster: Slimy is chilling in the kitchen sink as we speak.
PillowMonster: He always knows what's up around the house.
PillowMonster: When you spend all day in the plumbing, you pick things up, obviously.
-> Thanks!
    Robin: Thank you so much! You've been the biggest help!
    PillowMonster: Hmpf. You're welcome, little one.
-> (walk away)
// andere keuze dat je gewoon gelijk wegloopt miss?
===
